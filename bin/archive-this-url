#!/usr/bin/env perl
use v5.18;

use FindBin;
use lib "$FindBin::Bin/../lib";

use Diversion::BlobStore;

use DBI;
use HTTP::Tiny;
use JSON;
use IO::All;

my @feeds;
my $feed_url = shift or die "Missing URL in arg";
if (-f $feed_url) {
    @feeds = grep { s/\s//g; $_ } io($feed_url)->chomp->getlines;
} else {
    push @feeds, $feed_url;
}

my $dbh = DBI->connect(
    "dbi:SQLite:dbname=$ENV{HOME}/var/Diversion/url_archive/index.sqlite3",
    undef,
    undef,
    { AutoCommit => 1 }
);

my $blob_store = Diversion::BlobStore->new(
    root => "$ENV{HOME}/var/Diversion/blob_store/"
);

my $JSON = JSON->new->canonical->pretty;

my $sth = $dbh->prepare(q{ INSERT INTO uri_archive(uri, content_sha1_digest, header_sha1_digest) VALUES (?,?,?)});

for my $url (@feeds) {
    my $response = HTTP::Tiny->new->get($url);
    if ( $response->{status} eq '200' ) {
        my $header_dump_json = $JSON->encode($response->{headers});
        my $header_digest = $blob_store->put($header_dump_json);
        my $content_digest   = $blob_store->put($response->{content});
        $sth->execute($url, $content_digest, $header_digest);
        say "DONE $url";
    }
}
